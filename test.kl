PROGRAM  com_pc
%STACKSIZE = 32000
%NOPAUSE=ERROR+COMMAND+TPENABLE
%NOLOCKGROUP
%ENVIRONMENT ERRs
%ENVIRONMENT TIM
%ENVIRONMENT iosetup
%ENVIRONMENT fdev
%ENVIRONMENT STRNG
%ENVIRONMENT flbt


VAR 
status : INTEGER
entry : INTEGER
baglanti_var_mi:BOOLEAN
is_pause : INTEGER
file_var : FILE
prog_adi : STRING[50]
kaynak_sayisi : INTEGER
kaynak_basari:STRING[8]
kaynak_aninda :INTEGER
arc_on : BOOLEAN


--kaynak verileri
kaynak_saati :STRING[20]
toplam_sure : INTEGER
bas_satiri : INTEGER
bit_satiri : INTEGER
task_num:INTEGER
curr_line_stop:INTEGER
curr_line_start:INTEGER
kay_uzunlugu : STRING[20]
prc_no:INTEGER
sch_no:INTEGER

--anlik veriler
akim_ :STRING[20]
volt_ :STRING[20]
wfs_ :STRING[20]
power_ :STRING[20]
heat_input_:STRING[20]


--ERROR  DEĞİŞKENLERİ 
seq_num     : INTEGER
err_code    : INTEGER
err_str     : STRING[128]
cause_code  : INTEGER
cause_str   : STRING[128]
t_int       : INTEGER
t_str       : STRING[24]
severity    : INTEGER
prog_name   : STRING[64]
count : INTEGER


--get_var integer  fonk. --> başlangıç
ROUTINE get_var_int_getir(sys_var_name:STRING): INTEGER
VAR 
num : INTEGER
BEGIN
	GET_VAR(entry, '*SYSTEM*', sys_var_name, num, status)
RETURN (num)
END get_var_int_getir
--get_var integer  fonk.. --> bitiş


ROUTINE robot_durumu_yolla(file_var_:FILE ;kaynak_aninda_mi:INTEGER)
VAR 
kaynak_no :INTEGER

BEGIN
	kaynak_no = get_var_int_getir('$AWELOG[1].$AWCURLOG.$WELDSTAT.$AS_LINE') --başlangıç satırı getir
	seq_num = MAXINT
	ERR_DATA(seq_num, err_code, err_str, cause_code, cause_str, 
			t_int, severity, prog_name)
	WRITE file_var_(err_code,'|')
	WRITE file_var_(severity,'|')
	WRITE file_var_(err_str,'|')
	GET_TIME(t_int)
	CNV_TIME_STR(t_int, t_str)
	WRITE file_var_(t_str,'|')
	WRITE file_var_(kaynak_aninda_mi,'|')
	WRITE file_var_(kaynak_no,'|'::7) --kaynak no yollama işlemi
	WRITE file_var_('hataalindi'::10)

END robot_durumu_yolla


ROUTINE program_sifirla(file_var_:FILE ;kaynak_sayisi_:INTEGER;status:INTEGER)
BEGIN
		WRITE file_var_(kaynak_sayisi_)
		WRITE file_var_('progbitti'::9)
		DELAY 300
		CLOSE FILE file_var_
		MSG_DISCO('C3:',status)
		
END program_sifirla


--get_var string  fonk. --> başlangıç
ROUTINE get_var_str_getir(sys_var_name:STRING): STRING
VAR 
str : STRING[20]
BEGIN
	GET_VAR(entry, '*SYSTEM*', sys_var_name, str, status)
RETURN (str)
END get_var_str_getir
--get_var string  fonk.. --> bitiş


--akim voltaj tel surme hızı ve kaynak uzunluğu. satiri fonk. --> başlangıç
ROUTINE get_var_anlik(sys_var_name:STRING): STRING
VAR 
value : REAL
str_value : STRING[20]
length : INTEGER
num_digits : INTEGER

BEGIN
	GET_VAR(entry, '*SYSTEM*', sys_var_name, value, status)
	CNV_REAL_STR(value,length,num_digits,str_value)
	RETURN (str_value)
END get_var_anlik
--akim voltaj tel surme hızı ve kaynak uzunluğu. satiri fonk. --> başlangıç


--fonk. --> başlangıç
ROUTINE get_port_value(no1:INTEGER;no2:INTEGER) : BOOLEAN
VAR 
durum : INTEGER
BEGIN
	GET_PORT_VAL(no1, no2,durum,status)
	IF durum=1 THEN
		RETURN (TRUE)
	ELSE
		RETURN (FALSE)
	ENDIF
END get_port_value
--fonk.  --> bitiş


--fonk. --> başlangıç
ROUTINE baglanti_durumu_getir(file_var_:FILE) : BOOLEAN
VAR 
baglanti_durum : INTEGER
BEGIN
	baglanti_durum = IO_STATUS(file_var_)
	
	IF baglanti_durum = 0 THEN
		RETURN (TRUE)
	ELSE
		RETURN (FALSE)
	ENDIF


END baglanti_durumu_getir
--fonk.  --> bitiş


-- -----------------------------
-- MAIN PROGRAM
-- -----------------------------
BEGIN
    prog_adi = ''
    kaynak_saati = ''
	kaynak_sayisi = 0
    kaynak_aninda=0
    CLOSE FILE file_var	
    MSG_DISCO('C3:',status)
    GO TO program_dongusu

	program_dongusu::

		prog_adi = ''
		kaynak_saati = ''
		kaynak_aninda =0
		kaynak_sayisi =0
		arc_on = FALSE
		-- Program başladı mı kontrolü
		WHILE TRUE DO	
			task_num = get_var_int_getir('$SHELL_WRK.$TASK_NUM')
				IF (task_num <> 0) AND (UOUT[6] =OFF) THEN --prog başladı ve hata yok
					SET_FILE_ATR(file_var, ATR_IA)
					SET_VAR(entry, 
					'*SYSTEM*','$HOSTC_CFG[3].$SERVER_PORT',59002,status)
					MSG_CONNECT('C3:',status)
					OPEN FILE file_var('rw','C3:') 
					--prog ismi yollama
					IF (prog_adi = '') THEN
						prog_adi = get_var_str_getir('$SHELL_WRK.$ROUT_NAME')
						WRITE file_var(prog_adi::STR_LEN(prog_adi))
						GET_TIME(t_int)
						CNV_TIME_STR(t_int, t_str)
						WRITE file_var('|',t_str)
						WRITE file_var('RobotAktif'::10)
					ENDIF
					baglanti_var_mi = baglanti_durumu_getir(file_var)
					WRITE('Baglanti yapildi mi:',baglanti_var_mi,CR)
					IF baglanti_var_mi =TRUE THEN
						GO TO program_calisiyor
					ELSE
						MSG_DISCO('C3:', status)
						CLOSE FILE file_var
					ENDIF
				ENDIF
		DELAY 400
		ENDWHILE


	program_calisiyor::  --program başladı  kaynak bekleme alanı 

		WHILE TRUE DO
			DELAY 200
			task_num = get_var_int_getir('$SHELL_WRK.$TASK_NUM')
			is_pause = get_var_int_getir('$OPWORK.$INTPPAUSED')
			GET_VAR(entry, '*SYSTEM*', '$AWEPOR[1].$ARC_ON', arc_on, status)
			

			--Program başarılı bir şekilde tamamlandı  REGİON
			IF 	(task_num = 0) AND (UOUT[6] = OFF ) AND (is_pause = 0) AND (UOUT[5] =OFF) THEN
				program_sifirla(file_var,kaynak_sayisi,status) 
				GO TO program_dongusu --program yeniden başlatılırsa en başa dön
			ENDIF
			--Program başarılı bir şekilde tamamlandı ENDREGİON

			--Kaynak işlemi başladı ise girecek REGİON
			DELAY 200
			IF 	(arc_on = TRUE) AND (UOUT[6] =OFF) AND (is_pause = 0) AND (UOUT[5] =OFF) THEN 
				-- WRITE('yeni kaynak basladi',CR)
				GO TO weld_basladi
			ENDIF
			--Kaynak işlemi başladı ise girecek ENDREGİON

					
			--kaynak durdu veya pause basıldı veya hold basıldı veya hata varsa
			DELAY 250
			is_pause = get_var_int_getir('$OPWORK.$INTPPAUSED')
			IF  (is_pause <> 0) OR  (UOUT[5]=ON) OR (UOUT[6] = ON) THEN
				DELAY 3000 --fault değişimini 3 saniye bekliyoruz dout üzerinden aldığımızdan
				is_pause = get_var_int_getir('$OPWORK.$INTPPAUSED')
				GET_VAR(entry, '*SYSTEM*', '$AWEPOR[1].$ARC_ON', arc_on, status)
				robot_durumu_yolla(file_var,0) --pause bilgisi yolla
				WAIT FOR (UOUT[4] = OFF) --pause kalkmasını bekle
				GO TO program_calisiyor
			ENDIF
		DELAY 100
		ENDWHILE


	weld_basladi::
		
		WHILE TRUE DO 
			DELAY 250
			is_pause = get_var_int_getir('$OPWORK.$INTPPAUSED')
			GET_VAR(entry, '*SYSTEM*', '$AWEPOR[1].$ARC_ON', arc_on, status)
			

			--anlık verileri yolla
			IF 	(arc_on = TRUE) AND (UOUT[6] =OFF) AND (is_pause = 0)  AND (UOUT[5] =OFF) THEN
				akim_ =get_var_anlik('$AWEPOR[1].$AMPS_FDBK')
				volt_  = get_var_anlik('$AWEPOR[1].$VOLTS_FDBK')
				wfs_  =get_var_anlik('$AWEPOR[1].$WFS_FDBK')
				power_ =get_var_anlik('$AWEPOR[1].$POWER')
				heat_input_ =get_var_anlik('$AWEPOR[1].$HEAT_INPUT')
				WRITE file_var(volt_,'|')
				WRITE file_var(akim_,'|')
				WRITE file_var(wfs_,'|')
				WRITE file_var(power_,'|')
				WRITE file_var(heat_input_,'|')
				WRITE file_var('anlikveri'::9)

				baglanti_var_mi = baglanti_durumu_getir(file_var)	
				IF baglanti_var_mi = FALSE THEN
					-- BURADA ARTIK BİLİYORSUN Kİ C# İLE HABERLEŞME BOZULDU
					WRITE('anlik veriler yollanirken baglanti koptu',baglanti_var_mi,CR)
					MSG_DISCO('C3:', status)
					CLOSE FILE file_var
					-- Programı nasıl davranmasını istiyorsan öyle yönlendir:
					GO TO program_dongusu
				ENDIF


				DELAY 400
			
			ENDIF
			--anlık verileri yolla
			
			--kaynak durdu veya pause basıldı veya hold basıldı veya hata varsa
			IF (arc_on = FALSE) OR (is_pause <> 0) OR  (UOUT[5]=ON) OR (UOUT[6] = ON) THEN

				DELAY 2000 --fault değişimini 3 saniye bekliyoruz dout üzerinden aldığımızdan
				is_pause = get_var_int_getir('$OPWORK.$INTPPAUSED')
				GET_VAR(entry, '*SYSTEM*', '$AWEPOR[1].$ARC_ON', arc_on, status)

				IF (arc_on = FALSE)  AND (UOUT[5]=OFF) AND (is_pause <> 0)  AND (UOUT[6] =OFF) THEN --SHİFTTEN ELİNİ ÇEKTİ
					baglanti_var_mi = baglanti_durumu_getir(file_var)	
					robot_durumu_yolla(file_var,1) --pause bilgisi yolla
					WAIT FOR (UOUT[4] = OFF) --pause kalkmasını bekle
					GO TO weld_basladi
				ENDIF
				
				IF (arc_on = FALSE)  AND (UOUT[5]=ON) AND (is_pause <> 0)  AND (UOUT[6] =OFF) THEN --HOLD DURUMU
					robot_durumu_yolla(file_var,1) --hold bilgisi yolla
					WAIT FOR (UOUT[4] = OFF) --pause kalkmasını bekle
					GO TO weld_basladi
				ENDIF

				IF (arc_on = FALSE)  AND (UOUT[5]=OFF) AND (is_pause <> 0)  AND (UOUT[6] =ON) THEN --FAULT DURUMU
					GET_VAR(entry, '*SYSTEM*', '$SHELL_WRK.$CURR_LINE', curr_line_stop, status)  --Hata alınan satır
					robot_durumu_yolla(file_var,1) --FAULT bilgisi yolla
					WAIT FOR (UOUT[4] = OFF) --pause kalkmasını bekle
					GET_VAR(entry, '*SYSTEM*', '$SHELL_WRK.$CURR_LINE', curr_line_start, status)  --Hatadan sonra başlanan satır
					-- DELAY 500
					-- GET_VAR(entry, '*SYSTEM*', '$AWEPOR[1].$ARC_ON', arc_on, status) --arc_on açıksa kaynağa devam
					IF curr_line_stop = curr_line_start  THEN
						GO TO weld_basladi
					ELSE
						GO TO weld_basarisiz_bitti
					ENDIF
				ENDIF
				
				--kaynak başarılı bir şekilde tamamlandı
				IF 	(arc_on = FALSE) AND (UOUT[6] =OFF)  AND (is_pause = 0)  AND (UOUT[5] = OFF) AND (task_num <> 0)  THEN --weld başarılı bitti 
					-- WRITE('kaynaktan basarili cikis yapildi',CR)
					GO TO weld_basarili_bitti
				ENDIF
				--kaynak başarılı bir şekilde tamamlandı	
			ENDIF

			--program kaynak sırasında sıfırlanma durumunda kendini sıfırlasın
			task_num = get_var_int_getir('$SHELL_WRK.$TASK_NUM')
			IF ( task_num = 0) THEN
				-- WRITE ('program kaynaktayken tamamlanamadi',CR)
				program_sifirla(file_var,kaynak_sayisi,status)
				GO TO program_dongusu --program yeniden başlatılırsa en başa dön
			--program kaynak sırasında sıfırlanma durumunda kendini sıfırlasın
			ENDIF

		ENDWHILE


	weld_basarili_bitti::

		kaynak_basari='True'
		DELAY 100
		GO TO weld_bitti


	weld_basarisiz_bitti::

		kaynak_basari='False'
		GO TO weld_bitti


	weld_bitti::

		WAIT FOR (DOUT[201] = OFF)
		DELAY 100
		
		kaynak_saati = get_var_str_getir('$AWELOG[1].$AWCURLOG.$WELDSTAT.$TIME_STRING') --başlangıç saati
		toplam_sure = get_var_int_getir('$AWELOG[1].$AWCURLOG.$WELDSTAT.$WELD_TIME') --toplam kaynak sure
		kay_uzunlugu =get_var_anlik('$AWELOG[1].$AWCURLOG.$WELDSTAT.$WELD_DIST')  --Kaynak uzunlugu getir
		bas_satiri = get_var_int_getir('$AWELOG[1].$AWCURLOG.$WELDSTAT.$AS_LINE') --başlangıç satırı getir
		bit_satiri = get_var_int_getir('$AWELOG[1].$AWCURLOG.$WELDSTAT.$AE_LINE') --bitiş satırı getir
		prc_no = get_var_int_getir('$AWEPOR[1].$WLD_PRC_NUM') --prc no getir
		sch_no = get_var_int_getir('$AWEPOR[1].$WLD_SCH_NUM') --sch no getir	
		kaynak_sayisi = kaynak_sayisi + 1
		IF toplam_sure <= 0 THEN
			toplam_sure = 0
		ENDIF
		
		IF bas_satiri <= 0 THEN
			bas_satiri = 0
		ENDIF
		
		IF bit_satiri <= 0 THEN
			bit_satiri = 0
		ENDIF
		
		IF prc_no <= 0 THEN
			prc_no = 0
		ENDIF
		
		IF sch_no <= 0 THEN
			sch_no = 0
		ENDIF
		DELAY 100

		baglanti_var_mi = baglanti_durumu_getir(file_var)
		IF baglanti_var_mi = FALSE THEN
			-- BURADA ARTIK BİLİYORSUN Kİ C# İLE HABERLEŞME BOZULDU
			WRITE('kaynak bitisinde baglanti koptu',baglanti_var_mi,CR)
			MSG_DISCO('C3:', status)
			CLOSE FILE file_var
			-- Programı nasıl davranmasını istiyorsan öyle yönlendir:
			GO TO program_dongusu
		ENDIF

		WRITE file_var (kaynak_saati::20,'|'::3)
		WRITE file_var (toplam_sure::10, '|'::3)
		WRITE file_var (kay_uzunlugu::STR_LEN(kay_uzunlugu), '|')
		WRITE file_var (bas_satiri::4,'|'::3)
		WRITE file_var (bit_satiri::4, '|'::3)
		WRITE file_var (prc_no::2, '|'::3)
		WRITE file_var (sch_no::2, '|'::3)
		WRITE file_var (kaynak_basari,'|'::8)
		WRITE file_var (bas_satiri::4, '|') --kaynak numarası
		WRITE file_var ('KayOFF'::6)
		
		
		IF kaynak_basari = 'True' THEN
			-- WRITE('Kaynak basari ile tamamlandi',CR)
			GO TO program_calisiyor
		ELSE
			-- WRITE('Kaynak basarisiz tamamlandi',CR)
			GO TO program_calisiyor
		ENDIF
		

		DELAY 500
	--Program başladı mı araması -son 

END com_pc
--main --> bitiş